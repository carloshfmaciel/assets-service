plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.sonarqube" version "4.4.1.3373"
	id 'jacoco'
}

group = 'com.integrador'
version = '1.0.0-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-logging'
	implementation 'org.json:json:20210307'
	implementation 'com.google.code.gson:gson:2.8.9'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
	
	implementation 'net.javacrumbs.shedlock:shedlock-spring:5.10.0'
	implementation 'net.javacrumbs.shedlock:shedlock-provider-mongo:5.10.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	
	implementation 'com.google.cloud:spring-cloud-gcp-starter-pubsub:4.7.2'
	implementation 'org.springframework.integration:spring-integration-core:6.2.0'
	
	// UNIT TESTS
	testRuntimeOnly 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.mockito:mockito-junit-jupiter:5.8.0'
	testRuntimeOnly 'org.mockito:mockito-inline:5.2.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

sonarqube {
    properties {
    	property "sonar.projectKey", "ms-asset"
    	property "sonar.projectName", "ms-asset"
        property "sonar.login", "admin"
        property "sonar.password", "mills" 	
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/*.xml"
        property "sonar.java.coveragePlugin", "jacoco"
    }
}

// RODAR JACOCO REPORT APOS CADA BUILD
test.finalizedBy jacocoTestReport

jacocoTestReport {
	reports {
	    xml.required=true
	    csv.required=false
	    html.destination file("${buildDir}/jacocoHtml")
	}
	
	afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                "com/integrador/assets/AssetsApplication.class"
            ])
        }))
    }
}

// RODAR A VERIFICACAO DE COBERTURA MININA DE TESTE
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.1
            }
        }
    }
}
